apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

ext {
    bintrayRepo = 'com.dailymotion.dailymotion-sdk-android'
    bintrayName = 'sdk'

    publishedGroupId = 'com.dailymotion.dailymotion-sdk-android'
    libraryName = 'DailymotionPlayerSDKAndroid'
    artifact = 'sdk'

    libraryDescription = 'This SDK aims at easily embedding Dailymotion videos on your Android application using WebView.'

    siteUrl = 'https://github.com/dailymotion/dailymotion-player-sdk-android'
    gitUrl = 'https://github.com/dailymotion/dailymotion-player-sdk-android.git'

    libraryVersion = LibraryProject.INSTANCE.getLibraryVersionName()

    developerId = 'dmandroid'
    developerName = 'Dailymotion'
    developerEmail = 'apps@dailymotion.com'

    licenseName = 'MIT License'
    licenseUrl = 'https://github.com/dailymotion/dailymotion-player-sdk-android/blob/master/LICENSE'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion Version.compileSdkVersion

    defaultConfig {
        minSdkVersion Version.minSdkVersion
        targetSdkVersion Version.targetSdkVersion
        buildConfigField("String", "SDK_VERSION", "\"${LibraryProject.INSTANCE.getLibraryVersionName()}\"")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.android.gms:play-services-ads:16.0.0'

    implementation 'androidx.core:core-ktx:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply from: rootProject.file('gradle/install-v1.gradle')
apply from: rootProject.file('gradle/bintray-android-v1.gradle')

task bumpAndTag {
    doLast {
        LibraryProject.INSTANCE.tagAndIncrement(LibraryProject.INSTANCE.getLibraryVersionCode() + 1)
    }
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                version = LibraryProject.INSTANCE.getLibraryVersionName()
            }
        }
    }
}